#!/bin/fish
#----------------------------------------------------------------------------------------------------------------------------------------------------------
# Description: Find and remove orphaned snap packages unless they are default providers or bases; also remove disabled snaps.
# License: MIT. Copyright (c) 2024 Mario Herrmann.
#----------------------------------------------------------------------------------------------------------------------------------------------------------

# Use globals so functions can access them
set -g SCRIPT_NAME "orphand_snaps"
set -g INSTALL_PATH "/usr/local/bin/$SCRIPT_NAME"
set -g SERVICE_NAME "orphand-snaps.service"
set -g TIMER_NAME "orphand-snaps.timer"
set -g SYSTEMD_DIR "/etc/systemd/system"

# Choose elevation tool: doas or sudo. Override with OVERRIDE_ELEVATION=doas|sudo
function choose_elev --description 'Pick doas or sudo'
    if set -q OVERRIDE_ELEVATION
        if test "$OVERRIDE_ELEVATION" = "doas"
            echo doas
            return
        else if test "$OVERRIDE_ELEVATION" = "sudo"
            echo sudo
            return
        end
    end
    if type -q doas
        echo doas
    else if type -q sudo
        echo sudo
    else
        echo "Error: Neither sudo nor doas found in PATH." >&2
        exit 1
    end
end

# Install service and timer, copy script to /usr/local/bin
function install_units
    set -l ELEV (choose_elev)

    echo "Installing $SCRIPT_NAME to $INSTALL_PATH..."
    $ELEV install -m 0755 (status -f) $INSTALL_PATH

    echo "Creating systemd service and timer..."
    # Write service unit
    $ELEV bash -c 'cat > '"$SYSTEMD_DIR"'/'"$SERVICE_NAME"' <<SERVICE
[Unit]
Description=Remove orphaned and disabled snaps

[Service]
Type=oneshot
ExecStart=/usr/local/bin/orphand_snaps
Nice=10
IOSchedulingClass=best-effort
IOSchedulingPriority=7

[Install]
WantedBy=multi-user.target
SERVICE'
    # Write timer unit
    $ELEV bash -c 'cat > '"$SYSTEMD_DIR"'/'"$TIMER_NAME"' <<TIMER
[Unit]
Description=Weekly cleanup of orphaned and disabled snaps

[Timer]
OnCalendar=weekly
Persistent=true
AccuracySec=1h
Unit=orphand-snaps.service

[Install]
WantedBy=timers.target
TIMER'

    echo "Reloading systemd and enabling timer..."
    $ELEV systemctl daemon-reload
    $ELEV systemctl enable --now $TIMER_NAME

    echo "Installation complete. The timer is active:"
    systemctl list-timers $TIMER_NAME --no-legend 2>/dev/null
end

# Disable and remove service/timer and script
function uninstall_units
    set -l ELEV (choose_elev)

    echo "Disabling and removing systemd timer and service..."
    $ELEV systemctl disable --now $TIMER_NAME 2>/dev/null
    $ELEV systemctl disable $SERVICE_NAME 2>/dev/null
    $ELEV systemctl daemon-reload

    $ELEV rm -f "$SYSTEMD_DIR/$TIMER_NAME" "$SYSTEMD_DIR/$SERVICE_NAME"

    echo "Removing $INSTALL_PATH..."
    $ELEV rm -f "$INSTALL_PATH"

    echo "Uninstallation complete."
end

# Perform cleanup: remove disabled snaps, then orphaned snaps not default providers, then unused base snaps
function run_cleanup
    set -l ELEV (choose_elev)

    # Remove disabled snaps
    env LANG=C snap list --all | awk '/disabled/{print $1, $3}' | while read -l snapname revision
        $ELEV snap remove "$snapname" --revision="$revision"
    end

    # Find orphaned snaps and check if they are default providers
    set -l orphan (snap connections --all | string match -e 'content' | string match -v -r "themes|slot" | awk '$2 == "-" {print $3}' | string split -f 1 :)
    set -l provider (grep "default-provider:" /snap/*/*/meta/snap.yaml 2>/dev/null | awk '{print $NF}' | sort -u)

    if not set -q orphan[1]
        echo "No orphaned snaps found."
    else
        for o in $orphan
            set -l plug (snap connections "$o" | string match -e 'content' | awk '$2!= "-" {print $2}' | string split -f 1 :)
            if contains "$o" $provider
                echo "Snap $o is a default provider for $plug and cannot be removed."
            else
                # Remove orphaned snaps
                if not $ELEV snap remove "$o"
                    echo "Error removing snap $o."
                end
            end
        end
    end

    # Find base snaps and check if they are used by other snaps
    set -l corephan (snap list --all | awk '/base/{print $1}')
    set -l base (grep "base:" /snap/*/*/meta/snap.yaml 2>/dev/null | awk '{print $NF}' | sort -u)

    for b in $corephan
        set -l bplug (grep "base:" /snap/*/*/meta/snap.yaml 2>/dev/null | string split / -f 3 | awk '{print $NF}' | sort -u)
        if contains "$b" $base
            echo "$b is a base for $bplug and cannot be removed."
        else
            # Remove base snaps
            if not $ELEV snap remove "$b"
                echo "Error removing base snap $b."
            end
        end
    end
end

# Command dispatcher: determine subcommand and switch
set -l cmd ''
if test (count $argv) -ge 1
    set cmd $argv[1]
end

switch $cmd
case install
    install_units
case uninstall remove
    uninstall_units
case '*'
    run_cleanup
end